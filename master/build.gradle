buildscript {
  repositories{
    maven{
      url "https://raw.github.com/rodionmoiseev/maven-repo/master/repo/releases"
    }
  }
  dependencies {
    classpath group: 'org.rodion.gradle', name: 'idea-scala-plugin', version: '0.2'
  }
}

apply plugin: 'idea-scala-project'

subprojects { subproject ->
    ['scala', 'idea-scala-facet'].each { plugin -> subproject.apply plugin: plugin }
}

allprojects {
    apply plugin: 'eclipse'
    ext.scalaMajorVersion = '2.10'
    ext.scalaMinorVersion = '0'
    ext.scalaVersion = "${scalaMajorVersion}.${scalaMinorVersion}"
    dependencies {
        scalaApi "org.scala-lang:scala-library:${scalaVersion}"
        scalaApi "org.scala-lang:scala-reflect:${scalaVersion}"
        scalaTools "org.scala-lang:scala-compiler:${scalaVersion}"
    }
}


subprojects { subproject ->
    dependencies {
        compile 'ch.qos.logback:logback-classic:1.0.9'
        testCompile "org.scalatest:scalatest_${scalaVersion}:1.8"
        testCompile 'junit:junit-dep:4.11'
    }
}

project(':unfiltered-backend') {
    apply plugin: 'application'
    mainClassName = 'filter.Main'

    dependencies {
        compile project(':model')
        compile project(':slick-persistence')
        compile "net.databinder:unfiltered-filter_${scalaMajorVersion}:0.6.7"
        compile "net.databinder:unfiltered-jetty_${scalaMajorVersion}:0.6.7"
    }
}

project(':slick-persistence') {
    dependencies {
        compile project(':model')
        compile "com.typesafe:slick_${scalaMajorVersion}:1.0.0-RC2"
        compile "com.h2database:h2:1.3.170"
        compile 'com.googlecode.flyway:flyway-core:2.0.3'
        compile 'com.jolbox:bonecp:0.7.1.RELEASE'
    }
}

project(':model') {
    dependencies {
        compile "org.json4s:json4s-scalaz_${scalaMajorVersion}:3.1.0"
        compile "org.json4s:json4s-native_${scalaMajorVersion}:3.1.0"
        compile "org.json4s:json4s-ext_${scalaMajorVersion}:3.1.0"
    }
}
